# Maintainer: Henri Derycke <nheir.kim@gmail.com>
pkgname=bluez-alsa
pkgver=4.0.0
pkgrel=1
pkgdesc="Bluetooth Audio ALSA Backend"
arch=('x86_64' 'armv7h' 'aarch64' 'armv6h')
url="https://github.com/Arkq/bluez-alsa"
license=('MIT')
depends=('bluez-libs' 'glib2' 'libfdk-aac' 'sbc' 'alsa-lib')
makedepends=('git')

source=(https://github.com/Arkq/bluez-alsa/archive/v${pkgver}.tar.gz
        bluealsa.service
        bluealsa-aplay.service
        bluealsa.tmpfiles
        bluealsa.sysusers
        bluealsa.conf
        bluealsa-aplay.conf)

md5sums=('3332b7e05026ae18bf70876c8cb1fd7e'
         '2ad74853850bf5310dc29b42e0ffff6d'
         'eae2e823e2b04f1f4b7e636634a19620'
         'e049cc166f97f281bec3a43175849bdc'
         '0772f80cfbc72da4d090cd8811665727'
         'd055dca71e7616cdde7e20d89d8b452c'
         '2e913864bce5653bfe586c914aebdfd9')

install=bluealsa.install

build() {
    cd "$pkgname-$pkgver"
    autoreconf --install
    ./configure --prefix=/usr \
            --sysconfdir=/etc \
            --enable-aac      \
            --enable-mp3lame  \
            --enable-mpg123   \
            --enable-cli
    make -j4
}

package() {
    cd "$pkgname-$pkgver"
    make DESTDIR="$pkgdir/" install
    install -Dm0644 $srcdir/$pkgname-$pkgver/LICENSE $pkgdir/usr/share/licenses/$pkgname/LICENSE
    install -Dm0644 $srcdir/bluealsa.tmpfiles $pkgdir/usr/lib/tmpfiles.d/bluealsa.conf
    install -Dm0644 $srcdir/bluealsa.service $pkgdir/usr/lib/systemd/system/bluealsa.service
    install -Dm0644 $srcdir/bluealsa-aplay.service $pkgdir/usr/lib/systemd/system/bluealsa-aplay.service
    install -Dm0644 $srcdir/bluealsa.sysusers $pkgdir/usr/lib/sysusers.d/bluealsa.conf
    install -Dm0644 $srcdir/bluealsa.conf $pkgdir/etc/default/bluealsa
    install -Dm0644 $srcdir/bluealsa-aplay.conf $pkgdir/etc/default/bluealsa-aplay
}
