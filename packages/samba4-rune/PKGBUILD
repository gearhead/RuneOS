# Maintainer: Tobias Powalowski 
# Contributor: judd 
# Contributor: Michael Hansen 
# Contributor: Marco A Rojas 
# Contributor: Netanel Shine 
# Contributor: ngoonee 
# Contributor: Adam Russell 
# Contributor: Dhananjay Sathe 

pkgbase=samba
pkgname=('samba4-rune')
provides=('samba')
conflicts=('samba')
pkgver=4.7.6
pkgrel=1
arch=('x86_64' 'armv6h' 'armv7h')
url="http://www.samba.org"
license=('GPL3')
makedepends=('python2' 'docbook-xsl' 'pkg-config' 'libbsd' 'db' 'popt' 'libcups' 
             'readline' 'tevent' 'acl' 'libldap' 'libcap' 'ldb>=1.1.15' 'krb5' 'pam' 'systemd' 
             'gamin' 'gnutls>=2.4.1' 'talloc' 'tdb' 'dbus' 'libaio' 'perl-parse-yapp' 'jansson')
source=(http://us1.samba.org/samba/ftp/stable/${pkgbase}-${pkgver}.tar.gz
        
http://us1.samba.org/samba/ftp/stable/${pkgbase}-${pkgver}.tar.asc
        samba.logrotate
        samba.pam
        samba.service
        samba.conf.d
        smbd.service
        smbd.socket
        nmbd.service
        smbd@.service
        winbindd.service
        samba.conf
	smb-prod.conf
        smb-dev.conf)
validpgpkeys=('52FBC0B86D954B0843324CDC6F33915B6568B7EA') #Samba Distribution Verification Key 
### UNINSTALL dmapi package before building!!!

prepare() {
  cd samba-${pkgver}
  patch -p1 < "${startdir}"/0001-libads-Fix-the-build-without-ads.patch
}

build() {
  # Use samba-pkg as a staging directory for the split packages
  # (This is so RPATHS and symlinks are generated correctly via
  # make install, but the otherwise unsplit pieces can be split)
  _pkgsrc=${srcdir}/samba-pkg
  rm -rf ${_pkgsrc}
  cd ${srcdir}/samba-${pkgver}
  # change to use python2
  SAVEIFS=${IFS}
  IFS=$(echo -en "\n\b")
  PYTHON_CALLERS="$(find ${srcdir}/samba-${pkgver} -name '*.py')
$(find ${srcdir}/samba-${pkgver} -name 'wscript*')
$(find ${srcdir}/samba-${pkgver} -name 'configure.ac')
$(find ${srcdir}/samba-${pkgver} -name 'upgrade_from_s3')
$(find ${srcdir}/samba-${pkgver}/buildtools -type f)
$(find ${srcdir}/samba-${pkgver}/source4/scripting -type f)"
  sed -i -e "s|/usr/bin/env python$|/usr/bin/env python2|" \
         -e "s|python-config|python2-config|" \
         -e "s|bin/python|bin/python2|" \
      ${PYTHON_CALLERS}
  IFS=${SAVEIFS}

  export PYTHON=/usr/bin/python2
_samba4_idmap_modules=idmap_ad,idmap_rid,idmap_adex,idmap_hash,idmap_tdb2
_samba4_pdb_modules=pdb_tdbsam,pdb_ldap,pdb_ads,pdb_smbpasswd,pdb_wbc_sam,pdb_samba4
_samba4_auth_modules=auth_unix,auth_wbc,auth_server,auth_netlogond,auth_script,auth_samba4
 cd ${srcdir}/samba-${pkgver}
  ./configure --enable-fhs \
              --prefix=/usr \
              --sbindir=/usr/bin \
              --libdir=/usr/lib \
	      --libexecdir=/usr/lib/samba \
              --localstatedir=/var \
              --with-configdir=/etc/samba \
              --with-lockdir=/var/cache/samba \
              --with-sockets-dir=/var/run/samba \
              --with-piddir=/var/run \
              --without-ads \
              --without-ad-dc \
              --with-ldap \
              --with-winbind \
              --with-acl-support \
              --enable-gnutls \
              --with-pam \
              --with-pammodulesdir=/usr/lib/security \
              --bundled-libraries=!tdb,!talloc,!pytalloc-util,!tevent,!popt,!ldb,!pyldb-util \
              --with-shared-modules=${_samba4_idmap_modules},${_samba4_pdb_modules},${_samba4_auth_modules} \
              --disable-rpath-install

              # Add this to the options once it's working...
               #--with-system-mitkrb5 /opt/heimdal
  make
  make DESTDIR="${_pkgsrc}/" install

  # This gets skipped somehow
  if [ ! -e ${_pkgsrc}/usr/bin/smbtar ]; then
      install -m755 ${srcdir}/samba-${pkgver}/source3/script/smbtar ${_pkgsrc}/usr/bin/
  fi
}

package() {
pkgdesc="RuneAudio SMB Fileserver"
depends=('db>=4.7' 'popt' 'libcap>=2.16' 'gamin' 'gnutls>=2.4.1' 'jansson'
         'talloc' 'ldb' 'libbsd' 'python2' 'iniparser' 'tdb' 'libaio' 'perl-parse-yapp')
backup=(etc/logrotate.d/samba
        etc/pam.d/samba
        etc/samba/smb.conf
        etc/xinetd.d/swat
        etc/conf.d/samba)
install=samba.install
    # Use samba-pkg as a staging directory for the split packages
    # (This is so RPATHS and symlinks are generated correctly via
    # make install, but the otherwise unsplit pieces can be split)
    _pkgsrc=${srcdir}/samba-pkg
    # Everything that libwbclient and smbclient didn't install goes
    # into the samba package...
    mv ${_pkgsrc}/* ${pkgdir}/
    rmdir ${_pkgsrc}

    _pyver=`python2 -c 'import sys; print(sys.version[:3])'`

    find ${pkgdir}/usr/lib/python${_pyver}/site-packages/ -name '*.py' | \
         xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"
    find ${pkgdir}/usr/bin ${pkgdir}/usr/bin -type f -executable | \
         xargs sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|"

    # Make admin scripts look in the right place for the samba python module
    #for script in bin/samba_dnsupdate bin/samba_kcc bin/samba_spnupdate \
    #              bin/samba_upgradedns bin/samba-tool
    #do
    #    sed -i "/^sys\.path\.insert/ a\sys.path.insert(0, '/usr/lib/python${_pyver}/site-packages')" \
    #           ${pkgdir}/usr/${script}
    #done

  # install systemd files
  install -d -m755 ${pkgdir}/usr/lib/systemd/system
  #for i in smbd nmbd winbindd; do
  install -m644 ${srcdir}/*.service ${pkgdir}/usr/lib/systemd/system/
  install -m644 ${srcdir}/*.socket ${pkgdir}/usr/lib/systemd/system/
  install -d -m755  ${pkgdir}/etc/conf.d
  install -m644 ${srcdir}/samba.conf.d ${pkgdir}/etc/conf.d/samba
  #done
  # create ephemeral dirs via tmpfiles rather than shipping them in package
  install -D -m644 ${srcdir}/samba.conf ${pkgdir}/usr/lib/tmpfiles.d/samba.conf
  # install sample smb.conf
  install -d -m755 ${pkgdir}/etc/samba
  install -m644 ${startdir}/smb-prod.conf ${pkgdir}/etc/samba/smb-prod.conf
  install -m644 ${startdir}/smb-dev.conf ${pkgdir}/etc/samba/smb-dev.conf
  # make sym link to smb.conf

  mkdir -p ${pkgdir}/etc/samba/private
  chmod 700 ${pkgdir}/etc/samba/private

  install -D -m644 ${srcdir}/samba.logrotate ${pkgdir}/etc/logrotate.d/samba
  install -D -m644 ${srcdir}/samba.pam ${pkgdir}/etc/pam.d/samba 

  # winbind krb5 locator
  mkdir -p ${pkgdir}/usr/lib/krb5/plugins/libkrb5
  mv  ${pkgdir}/usr/lib/*.so ${pkgdir}/usr/lib/krb5/plugins/libkrb5/
  
  # spool directory
  install -d -m1777 ${pkgdir}/var/spool/samba
  
  rm -rf ${pkgdir}/var/run
  
  # copy ldap example
  install -D -m644 ${srcdir}/samba-${pkgver}/examples/LDAP/samba.schema ${pkgdir}/usr/share/doc/samba/examples/LDAP/samba.schema
  # create /var/run/samba
}
md5sums=('0253021a45c479cec1e135b004a0177a'
         'SKIP'
         '5697da77590ec092cc8a883bae06093c'
         '96f82c38f3f540b53f3e5144900acf17'
         'ee4763a656cf00d92bfda31b6bb2c5cb'
         '6c447748a064d631435dbef0a3dcf32f'
         'ecd46c1779b013cf794b09abe38ab0ad'
         'a78b9aa93eb14b0ac445897395693225'
         'c9bd3f75b9a52db1c436b5d58fdc94ec'
         '70afd2db60a2e8eff7615dbdecd887ac'
         'd5f56f43e1280bba20f6bbd88d16fa41'
         'a3ce057e8c6e4193198163ea13f21dac'
	 'e3fdd85d1c3789de7ab7f8414daf6d28'
	 '05ddbee8628b71f69b079a5c8cca8328')
