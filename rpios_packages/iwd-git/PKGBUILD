# Maintainer: mrdotx <klassiker@gmx.de>
# Contributor: Christian Rebischke <chris.rebischke@archlinux.org>
_pkgname='iwd'
pkgname=iwd-git
pkgver=0.r1.6315b42
pkgrel=2
pkgdesc='Internet Wireless Daemon'
arch=('i686' 'x86_64' 'armhf' 'arm64')
url='https://git.kernel.org/cgit/network/wireless/iwd.git/'
license=('LGPL')
depends=('libc6' 'libreadline8')
makedepends=('python3-docutils' 'dbus' 'systemd' 'libreadline-dev')
backup=('/etc/iwd/main.conf')
provides=('iwd')
conflicts=('iwd')
source=(
    'git+https://git.kernel.org/pub/scm/network/wireless/iwd.git'
    'git+https://git.kernel.org/pub/scm/libs/ell/ell.git'
    iwd-dbus.conf.diff
    0001-station-remove-scan-subsets-for-dbus-scans.patch
    0002-eap-fix-compile-issue-on-ARM-GCC-12.patch
    0003-scan-use-ifindex-and-only-active-scans.patch
)
sha256sums=('SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP'
            'SKIP')
changelog=ChangeLog
install=iwd.install

pkgver() {
    cd "$_pkgname"
    #git describe --long | sed -r 's/^r//;s/([^-]*-g)/r\1/;s/-/./g'
    printf "0.r%s.%s" "$(git rev-list --count HEAD)" "$(git rev-parse --short HEAD)"
}

prepare() {
    cd "$_pkgname"
    ./bootstrap
    patch -Np1 -i "${srcdir}/iwd-dbus.conf.diff"
# scan dsiconnect patches
    patch -Np1 -i "${srcdir}/0001-station-remove-scan-subsets-for-dbus-scans.patch"
    patch -Np1 -i "${srcdir}/0002-eap-fix-compile-issue-on-ARM-GCC-12.patch"
    patch -Np1 -i "${srcdir}/0003-scan-use-ifindex-and-only-active-scans.patch"
}

build() {
    cd "$_pkgname"
    dbus-run-session ./configure --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-runstatedir=/run \
        --libexecdir=/usr/libexec \
        --enable-wired \
        --enable-ofono \
        --disable-tools
    make
}

check() {
  cd "$_pkgname"
  # pass broken tests - should reported upstream
  # One test fail because we need kernel capabilities that
  # we don't have inside of systemd-nspawn
  # this could only be fixed via changing our build environment
  make -k check || /bin/true
}

package() {
    cd "$_pkgname"
    make install DESTDIR="$pkgdir"
    install -Dm 644 README -t "$pkgdir/usr/share/doc/$pkgname"
}
