# Maintainer: gearhead <ys3al35l at gmail dot com>
# Contributor: David Runge <dvzrv@archlinux.org>
# Contributor: Gaetan Bisson <bisson@archlinux.org>
# Contributor: Angel Velasquez <angvp@archlinux.org>
# Contributor: Andrea Scarpino <andrea@archlinux.org>
# Contributor: Damir Perisa <damir.perisa@bluewin.ch>
# Contributor: Ben <ben@benmazer.net>

pkgname=mpd
pkgver=0.23.12
pkgrel=1
pkgdesc="Flexible, powerful, server-side application for playing music"
arch=(armhf arm64)
url="https://www.musicpd.org/"
license=(GPL2)
depends=(
  adduser lsb-base libadplug-2.3.3-0 libao4 libasound2
  libaudiofile1 libavahi-client3 libavahi-common3 libavcodec58
  libavformat58 libavutil56 libbz2-1.0 libc6 libcdio-cdda2
  libcdio-paranoia2 libcdio19 libchromaprint1 libcurl3-gnutls
  libdbus-1-3 libexpat1 libfaad2 libflac8 libfluidsynth2
  libgcc-s1 libgme0 libicu67 libid3tag0 libiso9660-11
  libixml10 libjack-jackd2-0 libjack-0.125 libmad0
  libmikmod3 libmms0 libmodplug1 libmp3lame0
  libmpcdec6 libmpdclient2 libmpg123-0 libnfs13
  libogg0 libopenal1 libopus0 libpcre3 libpulse0
  libsamplerate0 libshout3 libsidplayfp5 libsmbclient
  libsndfile1 libsndio7.0 libsoxr0 libsqlite3-0 libstdc++6
  libsystemd0 libupnp13 liburing1 libvorbis0a libvorbisenc2
  libwavpack1 libwildmidi2 libyajl2 libzzip-0-13 zlib1g
  libjs-sphinxdoc libpipewire-0.3-0 libsidplay2 libsidutils0 
  libresid-builder0c2a
)
makedepends=(
  sphinx
  libfmt-dev
  libpcre2-dev
  libmad0-dev libmpg123-dev libid3tag0-dev
  libflac-dev libvorbis-dev libopus-dev libogg-dev
  libadplug-dev libaudiofile-dev libsndfile1-dev libfaad-dev
  libfluidsynth-dev libgme-dev libmikmod-dev libmodplug-dev
  libmpcdec-dev libwavpack-dev libwildmidi-dev
  libsidplay2-dev libsidutils-dev libresid-builder-dev
  libavcodec-dev libavformat-dev
  libmp3lame-dev libtwolame-dev libshine-dev
  libsamplerate0-dev libsoxr-dev
  libbz2-dev libcdio-paranoia-dev libiso9660-dev libmms-dev
  libzzip-dev
  libcurl4-gnutls-dev libyajl-dev libexpat-dev
  libasound2-dev libao-dev libjack-jackd2-dev libopenal-dev
  libpulse-dev libshout3-dev
  libsndio-dev
  libmpdclient-dev
  libnfs-dev
  libupnp-dev
  libavahi-client-dev
  libsqlite3-dev
  libsystemd-dev
  libgtest-dev
  libicu-dev
  libchromaprint-dev
  libgcrypt20-dev
  libboost-dev
  libpipewire-0.3-dev
)
backup=(/etc/$pkgname.conf)
source=(
  https://www.musicpd.org/download/$pkgname/${pkgver%.*}/$pkgname-$pkgver.tar.xz{,.sig}
  $pkgname.conf
  $pkgname.sysusers
  $pkgname.tmpfiles
  $pkgname.service.override
)
sha512sums=('54495b839d86b47ae6e2f6cf4e1baebd0e8eb924742e9db42cc7462ffc4b6f650b83f3eaea291fe905ff5f1975cf875537ffd23a111724013ce58e6df26ce36e'
            'SKIP'
            '25a823740d92da8e186916701413114142eb6ad91a172c592e68b569c8e4f50fa99580e555ccf6cd31fc4f55a09bfe0278efa46e4e76ee0fe02846292fadf3c1'
            '6e467481406279767b709ec6d5c06dbd825c0de09045c52ffa2d21d0604dcfe19b7a92bf42bed25163d66a3a0d1dbde6185a648b433eaf5eac56be90491e2e18'
            'db473db27cd68994c3ee26e78e0fb34d13126301d8861563dcc12a22d62ecb14c4ffb1e0798c6aaccdff34e73bae3fbeeff7b42606c901a2d35e278865cdf35d'
            'c1782b82f9db1d30aece43a07230c5d57370f2494a16e108af03815d83968805472f10f53ea5495cf0e08ff8f245430c3c3bc44025af43aaf9ecd12fcd6afc6c')
b2sums=('b218d9f477e92842638e3fba44c84957a3f656adaaf1ece998847db8d64883b8c997703206f74b37043888757f5f628799a3a35b25ea991f9ad00df1c4ad5e71'
        'SKIP'
        '0969a3c477b6a3f34b44e067e515d7f306414dd14e0163584417b9d071e3cc825898219f7ff66ead7905b15429b8411304052d3b2b14a72e560bfabf9bf0adcf'
        '4ab6e415284c77802a39d0913d701fe55e56f3c22b19557661fbef77e456b5e1d151da4202695282b956602e716a7afdb994aa2fc17368b9a0d0d051d47a3afb'
        'd7b587c25dd5830c27af475a8fdd8102139d7c8fdd6f04fe23b36be030e4411582e289f575c299255ff8183096f7d47247327276f9a24641cbd032d9675b837a'
        '753664445d7d5cc0b36f51ac66549beea403b9731cbcb81b0a782974a0a73d90559ba93e6afcaa470b6f2f5a844c09ef695bdf3b1e6dfee97aa080f41b7fe513')
validpgpkeys=('0392335A78083894A4301C43236E8A58C6DB4512') # Max Kellermann <max@blarg.de>

build() {
  local _meson_options=(
    -D documentation=enabled
    -D adplug=disabled
    -D sndio=disabled
    -D shine=disabled
    -D tremor=disabled
    -D b_ndebug=true
    -D sysconfdir='/etc' 
    -D prefix='/usr'
  )

  # NOTE: sndio conflicts with alsa
  # TODO: package adplug
  # TODO: package shine
  meson "${_meson_options[@]}" build $pkgname-$pkgver
  ninja -C build
}

check() {
  ninja -C build test
}

package() {
  DESTDIR="$pkgdir" ninja -C build install
  install -vDm 644 $pkgname-$pkgver/doc/${pkgname}conf.example -t "$pkgdir/usr/share/doc/$pkgname/"
  install -vDm 644 $pkgname.service.override "$pkgdir/usr/lib/systemd/system/mpd.service.d/00-arch.conf"
  install -vDm 644 $pkgname.conf -t "$pkgdir/etc/"
  install -vDm 644 $pkgname.sysusers "$pkgdir/usr/lib/sysusers.d/$pkgname.conf"
  install -vDm 644 $pkgname.tmpfiles "$pkgdir/usr/lib/tmpfiles.d/$pkgname.conf"
}

# vim: ts=2 sw=2 et:
