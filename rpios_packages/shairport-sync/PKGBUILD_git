# Maintainer: gearhead <ys3al35l at gmail dot com>

_pkgname=shairport-sync
pkgname=$_pkgname-rune
pkgver=4.3.2
pkgrel=3
pkgdesc="AirPlay 2 audio player with multi-room playback for Rune"
url="https://github.com/mikebrady/shairport-sync"
arch=('arm64' 'armhf' 'amd64')
license=('custom')
backup=(/etc/shairport-sync.conf)
depends=('openssl' 'libasound2' 'ffmpeg' 'libpulse0' \
         'mosquitto' 'libplist-utils' 'libconfig9')
makedepends=('autoconf' 'automake' 'libtool' 'xxd' 'libalac' 'mosquitto' 'ffmpeg' 'libasound2-dev' 'libsodium-dev' 'libplist-dev'\
             'libsoxr-dev' 'libjack-dev' 'portaudio19-dev' 'libmosquitto-dev' 'libpulse-dev'\
             'libgcrypt-dev' 'libavutil-dev' 'libavcodec-dev' 'libavformat-dev' 'libpopt-dev' 'libconfig-dev'\
             'libpipewire-0.3-dev' 'portaudio19-dev' 'libavahi-compat-libdnssd-dev')
optdepends=('pulseaudio: PulseAudio support'
            'pipewire: PipeWire support'
            'libsoxr: libsoxr-based resampling')
provides=('shairport-sync')
conflicts=('shairport-sync')
#source=(shairport-sync-$pkgver.zip::https://github.com/mikebrady/shairport-sync/archive/$pkgver.zip
source=("$_pkgname::git+https://github.com/mikebrady/shairport-sync.git"
        shairport-sync.sysusers
        remove_useradd.patch)
sha256sums=('SKIP'
            'bc2d92254910996e837d1c4c7dd81eddfb96a9f5f0cb2faad9fcb0414ea79a1d'
            '05325d35fca8f40118ee187dc06f3155bc23386ff7b73c5a5a968a5a4f4a9ff5')

prepare() {
# cd shairport-sync-$pkgver
  cd "$_pkgname"
  git checkout $pkgver
  patch -p1 < ../remove_useradd.patch
}

build() {
#  cd shairport-sync-$pkgver
  cd "$_pkgname"
  export PKG_CONFIG_PATH="/usr/lib/ffmpeg4.4/pkgconfig/:$PKG_CONFIG_PATH"

  autoreconf -i -f
  ./configure \
    --prefix=/usr \
    --sysconfdir=/etc \
    --with-alsa \
    --with-pa \
    --with-pw \
    --with-jack \
    --with-stdout \
    --with-pipe \
    --with-avahi \
    --with-pkg-config \
    --with-configfiles \
    --with-mqtt-client \
    --with-ssl=openssl \
    --with-metadata \
    --with-apple-alac \
    --with-soxr \
    --with-systemd \
    --with-dbus-interface \
    --with-airplay-2
  make -j4
}

package() {
#  cd shairport-sync-$pkgver
  cd "$_pkgname"
  make DESTDIR="$pkgdir" install
  install -D -m644 "$srcdir"/shairport-sync.sysusers "$pkgdir"/usr/lib/sysusers.d/shairport-sync.conf
  install -D -m644 LICENSES "$pkgdir"/usr/share/licenses/$_pkgname/LICENSE
  rm "$pkgdir"/etc/shairport-sync.conf.sample
}
